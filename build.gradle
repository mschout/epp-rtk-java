plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'epp-rtk'
version = '0.9.9'

dependencies {
    implementation 'bouncycastle:bcprov-jdk16:140'
    implementation 'log4j:log4j:1.2.17'
    implementation 'regexp:regexp:1.3'
    implementation 'xerces:xerces:2.4.0'
}

repositories {
    mavenCentral()
}

java {
  withSourcesJar()
  withJavadocJar()
}

signing {
    useGpgCmd()
    sign publishing.publications
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId    = 'io.github.mschout'
            artifactId = 'epp-rtk-java'
            version    = '0.9.9'

            from components.java

            pom {
                name = "${groupId}:${artifactId}"
                description = 'Fork of EPP RTK Java'
                url = 'https://github.com/mschout/epp-rtk-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 1.1'
                        url = 'https://www.apache.org/licenses/LICENSE-1.1'
                    }
                }
                developers {
                    developer {
                        name = 'Michael Schout'
                        email = 'schoutm@gmail.com'
                        organizationUrl = 'https://github.com/mscnout'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mschout/epp-rtk-java.git'
                    developerConnection = 'scm:git:ssh://github.com:mschout/epp-rtk-java.git'
                    url = 'https://github.com/mschout/epp-rtk-java/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME") ?: 'credentials'
                password = System.getenv("OSSRH_PASSWORD") ?: 'credentials'
            }
        }
    }
}
